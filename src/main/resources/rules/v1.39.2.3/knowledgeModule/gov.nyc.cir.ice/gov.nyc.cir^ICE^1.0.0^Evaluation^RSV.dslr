/**
 * Copyright (C) 2022 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package knowledgeModule.gov.nyc.cir.ice

import java.util.List
import java.util.Set
import org.drools.core.spi.KnowledgeHelper
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

//declare any global variables here
global java.util.Date evalTime
global org.joda.time.MonthDay rsvSeasonStartMonthDay
global org.joda.time.MonthDay rsvSeasonEndMonthDay
global java.lang.Boolean isRSVHighRisk
global java.lang.Boolean wasMommyVaxGiven


rule "RSV: Evaluate the RSV Injection as Outside RSV Season if it does not fall within the Season Start and Stop Dates"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseInSeasonCheck"
	salience 50
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- make note of the date this shot was administered as $currentShotDate
			- make note of the Associated Series as $associatedTargetSeries
    The Given date $currentShotDate does not fall within the ranges of rsvSeasonStartMonthDay and rsvSeasonEndMonthDay
	then
		Include the reason for shot $currentShot Invalid due to "Outside RSV Season"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end


rule "RSV(Synagis): If Given Synagis and patient is NOT on RSV High risk, evaluate it as INVALID dose"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "doseInSeasonCheck"
	salience 30
	when
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE93")
			- make note of the Associated Series as $associatedTargetSeries
		Confirm the variable isRSVHighRisk is != true
	then
		Include the reason for shot $currentShot Invalid due to "Not Recommended unless High Risk"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV: (Beyfortus) If the vaccine was administered for a patient that is >= 20 months old invalidate vaccine"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "validDoseCheck"
	salience 30
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt20mOfAge when the patient is "20m" of age
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE306", "ICE307")
			- the administration date of the shot is >= $dateAt20mOfAge
			- make note of the associated series as $associatedTargetSeries
	then
		//Include the reason for shot $currentShot Invalid due to "Above Recommended Age for Series"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV(Beyfortus): Invalidate if patient is not in high risk and Beyfortus previous valid vaccine was given"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	activation-group "validDoseCheck"
  salience 10
	when
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the date this shot was administered as $currentShotAdministrationDate
			- make note of the associated series as $associatedTargetSeries
		There is an Administered Shot $previousBeyfortusShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE306", "ICE307")
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- the shot is not ignored
			- the administration date of the shot is < $currentShotAdministrationDate
		Confirm the variable isRSVHighRisk is != true
	then
		Set the shot status of $currentShot to Invalid
		Include the reason for shot $currentShot Invalid due to "Not Recommended unless High Risk"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV: Invalidate same season vaccine if the Beyfortus vaccine was administered and valid on the same RSV season"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	activation-group "validDoseCheck"
  salience 29
	when
		There is an Administered Shot $currentShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- that has already been evaluated and whose shot validity is VALID or ACCEPTED
			- make note of the date this shot was administered as $currentShotAdministrationDate
			- make note of the associated series as $associatedTargetSeries
		There is an Administered Shot $previousBeyfortusShot
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE306", "ICE307")
			- the shot is not ignored
			- the administration date of the shot is < $currentShotAdministrationDate
			- make note of the date this shot was administered as $previousBeyfortusShotDate
		Confirm Elapsed time between $previousBeyfortusShotDate and $currentShotAdministrationDate < "6m"
	then
		Set the shot status of $currentShot to Invalid
		Include the reason for shot $currentShot Invalid due to "Below Minimum Interval"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV: (Beyfortus) If the vaccine was administered for a patient that is >= 8 months old and rsv not indicated, invalid dose"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "validDoseCheck"
  salience 20
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt8mOfAge when the patient is "8m" of age
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE307")
			- the administration date of the shot is >= $dateAt8mOfAge
			- make note of the associated series as $associatedTargetSeries
		Confirm the variable isRSVHighRisk is != true
	then
		Include the reason for shot $currentShot Invalid due to "Not Recommended unless High Risk"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV(Beyfortus): If the vaccine was administered on season and mommy vax was correctly given, Invalidate Vaccine"
	agenda-group "HistoryEvaluation^customEvaluationRule"
	activation-group "validDoseCheck"
  salience 18
	when
		The patient information $oEvaluatedPerson must be known to complete writing this rule
			- make note of the date as $dateAt8mOld when the patient is "8m" of age
			- make note of the Patient's birthdate as $birthdate
		There is an Administered Shot $currentShot that needs to be evaluated
			- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
			- the vaccine administered a member of ("ICE306", "ICE307", "ICE93")
			- the administration date of the shot is < $dateAt8mOld
			- make note of the associated series as $associatedTargetSeries
			- make note of the date this shot was administered as $currentShotDate
    The Given date $currentShotDate falls within the ranges of rsvSeasonStartMonthDay and rsvSeasonEndMonthDay
    Confirm the variable wasMommyVaxGiven is == true
	then
		Include the reason for shot $currentShot Invalid due to "Not Recommended due to Mother Vaccine Documented"
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end

rule "RSV(Beyfortus): If the vaccine was administered below minimum interval from previous valid Synagis, revalidate vaccine"
	agenda-group "HistoryEvaluation^postEvaluationCheck"
	no-loop true
  salience 980
	when
		There is an Administered Shot $currentShot
    	- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
    	- that has already been evaluated and whose shot validity is INVALID
    	- the vaccine administered a member of ("ICE306", "ICE307")
    	- make note of the date this shot was administered as $currentShotAdministrationDate
    	- make note of the associated series as $associatedTargetSeries
    There is an IceFact $currentShotFact
    	- that has associated administered shot $currentShot
    	- that has finding SupportedFactConcept._BELOW_MINIMUM_INTERVAL.conceptCodeValue
    There is an Administered Shot $previousSynagisShot
    	- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
    	- the vaccine administered a member of ("ICE93")
    	- that has already been evaluated and whose shot validity is VALID or ACCEPTED
    	- the shot is not ignored
    	- the administration date of the shot is < $currentShotAdministrationDate
    	- make note of the date this shot was administered as $previousSynagisShotDate
    There does not exist an administered shot
    	- the shot belongs to the vaccine group "VACCINE_GROUP_CONCEPT.905"
    	- that has already been evaluated and whose shot validity is VALID or ACCEPTED
    	- the shot is not ignored
    	- the administration date of the shot is < $currentShotAdministrationDate
    	- the administration date of the shot is > $previousSynagisShotDate
	then
	  Remove all evaluation reasons from shot $currentShot
    Set the Shot Status of $currentShot to Valid
    Refresh all facts in the shot $currentShot
		Record that this dose rule was Processed for the TargetDose $currentShot
		Log that this dose rule fired for the dose $currentShot in the Series $associatedTargetSeries
end
