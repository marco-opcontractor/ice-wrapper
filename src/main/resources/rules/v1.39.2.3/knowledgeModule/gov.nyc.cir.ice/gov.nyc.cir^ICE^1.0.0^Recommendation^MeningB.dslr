/**
 * Copyright (C) 2023 New York City Department of Health and Mental Hygiene, Bureau of Immunization
 * Contributions by HLN Consulting, LLC
 *
 * This program is free software: you can redistribute it and/or modify it under the terms of the GNU
 * Lesser General Public License as published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version. You should have received a copy of the GNU Lesser
 * General Public License along with this program. If not, see <http://www.gnu.org/licenses/> for more
 * details.
 *
 * The above-named contributors (HLN Consulting, LLC) are also licensed by the New York City
 * Department of Health and Mental Hygiene, Bureau of Immunization to have (without restriction,
 * limitation, and warranty) complete irrevocable access and rights to this project.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; THE
 *
 * SOFTWARE IS PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING,
 * BUT NOT LIMITED TO, WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
 * NONINFRINGEMENT. IN NO EVENT SHALL THE COPYRIGHT HOLDERS, IF ANY, OR DEVELOPERS BE LIABLE FOR
 * ANY CLAIM, DAMAGES, OR OTHER LIABILITY OF ANY KIND, ARISING FROM, OUT OF, OR IN CONNECTION WITH
 * THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
 *
 * For more information about this software, see http://www.hln.com/ice or send
 * correspondence to ice@hln.com.
 */

package knowledgeModule.gov.nyc.cir.ice

import java.util.List
import java.util.Set
import java.util.Date
import org.opencds.vmr.v1_0.internal.EvalTime
import org.opencds.vmr.v1_0.internal.EvaluatedPerson
import org.cdsframework.ice.service.DiseaseImmunity
import org.cdsframework.ice.service.DoseRule
import org.cdsframework.ice.service.DoseStatus
import org.cdsframework.ice.service.ICEFactTypeFinding
import org.cdsframework.ice.service.ICELogicHelper
import org.cdsframework.ice.service.Recommendation
import org.cdsframework.ice.service.RecommendationStatus
import org.cdsframework.ice.service.Schedule
import org.cdsframework.ice.service.SeriesRules
import org.cdsframework.ice.supportingdatatmp.SupportedFactConcept
import org.cdsframework.ice.service.TargetDose
import org.cdsframework.ice.service.TargetSeries
import org.cdsframework.ice.util.TimePeriod
import org.cdsframework.ice.util.TimePeriod.DurationType
import org.cdsframework.ice.service.Vaccine

expander ../../knowledgeCommon/org.cdsframework.ice/org.cdsframework^ICE^1.0.0.dsl

global java.util.Date evalTime
global org.cdsframework.ice.service.Schedule schedule
global java.lang.Boolean isMenBSharedDecision
global java.lang.Boolean isMenBHighRisk
global java.lang.Boolean isMenBTrumemba2Series
global java.lang.Boolean isMenBTrumemba3Series
global java.lang.Boolean isMenBTrumembaSeriesChanged


/*************************************************************************************************************************************************************************************
Recommending at the Vaccine vs. Vaccine Group Level; including additional reason codes where necessary
*************************************************************************************************************************************************************************************/

// If no doses have been administered, then do not recommend a specific vaccine
rule "Mening B: Do not recommend a specific vaccine if no (valid) doses have been administered"
	dialect "mvel"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- post processing on the Series forecast has not already been run
			- a forecast for the Series has been made and a Specific Vaccine is Recommended
			- the effective dose number in the Series is == 1
	then
		// Unset the Recommended Vaccine for the forecast in the Series $targetSeries
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// When recommending in the MenB 4C 2-dose Series, recommend CVX 163 for the next target dose.
rule "Mening B: Recommend CVX 163 for the 2nd target dose in the MenB 4C 2-dose Series"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the name of the series is "MenB4C2DoseSeries"
			
	then
		//Create a Recommendation as $oRecommendation for the Series $targetSeries
		//Set the Recommendation Vaccine for $oRecommendation to "ICE163"
		//Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// For any of the Mening B series, if patient has at least one CVX 162 (Meningococcal B FHbp, recombinant (Trumenba)) and at least one CVX 163 (Meningococcal B 4C, OMV (Bexsero)) on record, 
// recommend CVX 163 and include the reason codes: DUE_NOW or DUE_IN_FUTURE and OTHER_VACCINE_PRODUCT_POSSIBLE.
rule "Mening B: If a patient has at least one CVX 162 and at least one CVX 163 and a shot is recommended, include a recommendation with reason OTHER_VACCINE_PRODUCT_POSSIBLE"
	dialect "mvel"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	when
		There is a Series $targetSeries
			- a Forecast for the Series has been made and a shot is recommended
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- post processing on the Series forecast has not already been run
		There exists an administered shot
			- the shot belongs to the series $targetSeries
			- the vaccine administered is "ICE162"
		There exists another administered shot
			- the shot belongs to the series $targetSeries
			- the vaccine administered is "ICE163"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Reason for $oRecommendation to "RECOMMENDATION_REASON_CONCEPT.OTHER_VACCINE_PRODUCT_POSSIBLE"
		Include the Recommendation $oRecommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// When recommending in the MenB FHbp 2-dose Series, recommend CVX 162 for the next target dose.
rule "Mening B: Recommend CVX 162 for the next target dose in the MenB FHbp 3-dose Series"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the name of the series is "MenBFHbp3DoseSeries"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation to "ICE162"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// When recommending in the MenB FHbp 3-dose Series, recommend CVX 162 for the next target dose.
rule "Mening B: Recommend CVX 162 for the next target dose in the MenB FHbp 2-dose Series"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the name of the series is "MenBFHbp2DoseSeries"
	then
		Create a Recommendation as $oRecommendation for the Series $targetSeries
		Set the Recommendation Vaccine for $oRecommendation to "ICE162"
		Include the Recommendation $oRecommendation for Consideration in the final forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end

rule "Mening B: Ensure after invalid dose of MenB Trumemba 2-Series at least 4 months passed to recommend the a new dose"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	salience 980
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- a forecast for the Series has been made and a shot is Recommended
      - Make note of the earliest date as $earliestDate
			- the Series is not Complete
			- the effective dose number in the Series is == 2

    // Previous Shot (Invalid Dose) Conditions
    There is an administered shot $previousShot
      - the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
      - that has already been evaluated and whose shot validity is INVALID
      - the vaccine administered is "ICE162"
      - make note of the date this shot was administered as $previousShotDate
    Confirm the variable isMenBTrumemba2Series is == true
	then
		Add "4m" to $previousShotDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $recommendation with Status RecommendationStatus.RECOMMENDED for the Series $targetSeries
		Set the Final recommendation earliest forecast date on series $targetSeries to $dtCalculated
		Set the Final recommendation recommended forecast date on series $targetSeries to $dtCalculated
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
    Log that this Series Rule fired for the Series $targetSeries
end

//rule "Mening B: When MenBTrumemba2Series end forecasting upon second valid vaccine 6 months apart"
//	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
//	salience -10
//	no-loop true
//	when
//		There is a Series $associatedTargetSeries identified by $targetSeries
//			- the effective dose number in the Series is == 2
//		There is an Administered Shot $dose2
//			- the Shot belongs to the Series $targetSeries
//			- that has already been Evaluated and whose Shot Validity is VALID
//			- Make note of the Date this Shot was Administered as $dtAdministrationDate2
//		Confirm elapsed time between $dtAdministrationDate1 and $dtAdministrationDate2 >= "6m"
//    Confirm the variable isMenBTrumemba2Series is == true

//	then
//	  Clear forecasted recommendations from consideration in series $targetSeries
//		Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
//    Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.COMPLETE"
//    Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
//    Mark Forecasting of the Series $targetSeries Complete
//		Record that this Series Rule was Processed for the TargetSeries $targetSeries
//    Log that this Series Rule fired for the Series $targetSeries
//end

/*************************************************************************************************************************************************************************************
END Recommending at the Vaccine vs. Vaccine Group Level; including additional reason codes where necessary
*************************************************************************************************************************************************************************************/


/*************************************************************************************************************************************************************************************
Recommendation Rules based on Patient Age
If no doses are on record and
    + the patient is < 10 years old, then the Recommendation is Not Recommended and the reason code is BELOW_MINIMUM_AGE_HIGH_RISK_SERIES. (New recommendation code)
    + the patient is >= 10 years old and < 16 years old, then the Recommendation is Conditional and the reason code is HIGH_RISK.
    + the patient is >= 16 years old and < 24 years old, then the Recommendation is Conditional and the reason code is CLINICAL_PATIENT_DISCRETION.(New recommendation code)
    + the patient is >= 24 years old, then the Recommendation is Conditional and the reason code is TOO_OLD_HIGH_RISK.
*************************************************************************************************************************************************************************************/

// If no doses are on record and the patient is < 10 years old, then the Recommendation is Not Recommended and the reason code is BELOW_MINIMUM_AGE_HIGH_RISK_SERIES.
rule "Mening B: If the patient < 10yrs and no doses on record, recommendation is NOT_RECOMMENDED/BELOW_MINIMUM_AGE_HIGH_RISK_SERIES"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the effective dose number in the Series is == 0
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and evalTime < "10y"
	then
		Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.BELOW_MINIMUM_AGE_HIGH_RISK_SERIES"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

rule "Mening B: If the patient is in high risk group, start recommending since 10 years through 23 years"
	dialect "mvel"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the effective dose number in the Series is == 0
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and evalTime >= "10y"
		Confirm elapsed time between $birthdate and evalTime < "23y"
		Confirm the variable isMenBHighRisk is == true
	then
	  Clear forecasted recommendations from consideration in series $targetSeries
		Create a Recommendation as $recommendation with Status RecommendationStatus.RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.MENB_HIGH_RISK"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

rule "Mening B(Abstract): The series forecasting right now is for high risk"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	salience -10
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the name of the series is "MenB4C2DoseSeries"
			- a forecast for the Series has been made and a shot is Recommended
			- the Series is not Complete
			//- the effective dose number in the Series is == 1
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
			- make note of the date as $dtDateAtAge10y when the patient is "10y" of age
			- make note of the date as $dtDateAtAge16y when the patient is "16y" of age
		There is an Administered Shot $dose1
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 1
			- that has already been Evaluated and whose Shot Validity is VALID
			- the administration date of the shot is >= $dtDateAtAge10y
			- the administration date of the shot is < $dtDateAtAge16y
			- Make note of the Date this Shot was Administered as $dtAdministrationDate1
		//Confirm the variable isMenBHighRisk is == true
	then
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
    Log that this Series Rule fired for the Series $targetSeries
end

rule "Mening B: When at high risk, end forecasting upon second valid vaccine 6 months apart" extends
    "Mening B(Abstract): The series forecasting right now is for high risk"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	salience -10
	no-loop true
	when
		There is a Series $associatedTargetSeries identified by $targetSeries
			- the effective dose number in the Series is == 3
		There is an Administered Shot $dose2
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 2
			- that has already been Evaluated and whose Shot Validity is VALID
			- Make note of the Date this Shot was Administered as $dtAdministrationDate2
		Confirm elapsed time between $dtAdministrationDate1 and $dtAdministrationDate2 >= "6m"
	then
	  Clear forecasted recommendations from consideration in series $targetSeries
		Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
    Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.COMPLETE"
    Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
    Mark Forecasting of the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
    Log that this Series Rule fired for the Series $targetSeries
end

rule "Mening B: If the patient is in shared decision making group, start recommending since 16 years through 23 years"
	dialect "mvel"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the effective dose number in the Series is == 0
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and evalTime >= "16y"
		Confirm elapsed time between $birthdate and evalTime < "23y"
		Confirm the variable isMenBSharedDecision is == true
	then
	  Clear forecasted recommendations from consideration in series $targetSeries
		Create a Recommendation as $recommendation with Status RecommendationStatus.RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.MENB_SHARED_DECISION"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end


rule "Mening B: If the patient >= 24yrs and no doses on record, recommendation is N/A"
	dialect "mvel"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs forecasting
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the effective dose number in the Series is == 0
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
		Confirm elapsed time between $birthdate and evalTime >= "24y"
	then
		Clear forecasted recommendations from consideration in series $targetSeries
		Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
		Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.MENB_SHARED_DECISION_NOT_APPLICABLE"
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
		Log that this Series Rule fired for the Series $targetSeries
end

rule "Mening B: Ensure that third dose is at least 6 months apart from first dose"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	salience -5
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the name of the series is "MenB4C2DoseSeries"
			- a forecast for the Series has been made and a shot is Recommended
      - Make note of the earliest date as $earliestDate
			- the Series is not Complete
			- the effective dose number in the Series is == 3
		There is an Administered Shot $dose1
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 1
			- that has already been Evaluated and whose Shot Validity is VALID
			- Make note of the Date this Shot was Administered as $dtAdministrationDate
	then
		Add "6m" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Create a Recommendation as $recommendation with Status RecommendationStatus.RECOMMENDED for the Series $targetSeries
		Calculate the latter of $dtCalculated and $earliestDate and save calculated date as $earliestMenBDate
		Set the Final recommendation earliest forecast date on series $targetSeries to $earliestMenBDate
		Set the Final recommendation recommended forecast date on series $targetSeries to $earliestMenBDate
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
    Log that this Series Rule fired for the Series $targetSeries
end


rule "Mening B(Abstract): The series forecasting right now is for shared decision making"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	salience -10
	no-loop true
	when
		There is a Series $targetSeries
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the name of the series is "MenB4C2DoseSeries"
			- a forecast for the Series has been made and a shot is Recommended
			- the Series is not Complete
			- the effective dose number in the Series is > 1
		The Patient information $patientInfo must be known to complete writing this rule
			- make note of the Patient's birthdate as $birthdate
			- make note of the date as $dtDateAtAge16y when the patient is "16y" of age
			- make note of the date as $dtDateAtAge23y when the patient is "23y" of age
		There is an Administered Shot $dose1
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 1
			- that has already been Evaluated and whose Shot Validity is VALID
			- the administration date of the shot is >= $dtDateAtAge16y
			- the administration date of the shot is < $dtDateAtAge23y
			- Make note of the Date this Shot was Administered as $dtAdministrationDate1
		Confirm the variable isMenBHighRisk is == false
	then
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
    Log that this Series Rule fired for the Series $targetSeries
end

rule "Mening B: When at shared decision making, end forecasting upon second valid vaccine 6 months apart" extends
    "Mening B(Abstract): The series forecasting right now is for shared decision making"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	salience -10
	no-loop true
	when
		There is a Series $associatedTargetSeries identified by $targetSeries
			- the effective dose number in the Series is == 3
		There is an Administered Shot $dose2
			- the Shot belongs to the Series $targetSeries
			- the Dose Number in the Series is == 2
			- that has already been Evaluated and whose Shot Validity is VALID
			- Make note of the Date this Shot was Administered as $dtAdministrationDate2
		Confirm elapsed time between $dtAdministrationDate1 and $dtAdministrationDate2 >= "6m"
	then
	  Clear forecasted recommendations from consideration in series $targetSeries
		Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
    Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.COMPLETE"
    Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
    Mark Forecasting of the Series $targetSeries Complete
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
    Log that this Series Rule fired for the Series $targetSeries
end


rule "Mening B: When at shared decision making, second dose recommendation is 6 months apart" extends
    "Mening B(Abstract): The series forecasting right now is for shared decision making"
	agenda-group "RecommendationForecast^postCustomRecommendationCheck"
	salience -10
	no-loop true
	when
		There is a Series $associatedTargetSeries identified by $targetSeries
			- the effective dose number in the Series is == 2
	then
	  Clear forecasted recommendations from consideration in series $targetSeries
	  Create a Recommendation as $recommendation with Status RecommendationStatus.RECOMMENDED for the Series $targetSeries
		Add "6m" to $dtAdministrationDate1 and make note of the newly calculated date as $dtCalculated
		Set the Recommendation Recommended Forecast Date for $recommendation to $dtCalculated
		Set the Recommendation Earliest  Forecast Date for $recommendation to $dtCalculated
		Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
    Log that this Series Rule fired for the Series $targetSeries
end


/*************************************************************************************************************************************************************************************
MenB FHbp 3-dose Series specific rules
*************************************************************************************************************************************************************************************/

// Recommended interval between Dose 1 and Dose 3 in Men B FHbp 3-dose series is 6 months
rule "MeningB: Recommend an recommended interval of at 6 months between doses 1 & 3 in FHbp 3-dose Series"
	agenda-group "RecommendationForecast^customRecommendationRule"
	when
		There is a Series $targetSeries that needs Forecasting
			- the name of the Series is "MenBFHbp3DoseSeries"
			- the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
			- the Series is not Complete
			- the effective dose number in the Series is == 3
		There is an Administered Shot $dose1
			- the Shot belongs to the Series $targetSeries 
			- the Dose Number in the Series is == 1
			- that has already been Evaluated and whose Shot Validity is VALID
			- Make note of the Date this Shot was Administered as $dtAdministrationDate
	then
		Add "6m" to $dtAdministrationDate and make note of the newly calculated date as $dtCalculated
		Include a Recommendation as $r with Recommended Forecast Date $dtCalculated for consideration in the final Forecast of the Series $targetSeries
		Set the Recommendation Earliest Forecast Date for $r to $dtCalculated
		Record that this Series Rule was Processed for the TargetSeries $targetSeries
       	Log that this Series Rule fired for the Series $targetSeries
end


// Recommend earliest inverval of 6 months between Dose 1 and Dose 3 in Men B FHbp 3-dose series
// Nothing to do - since minimum interval and recommended interval is the same (6m); handled by above rule


/*************************************************************************************************************************************************************************************
END MenB FHbp 3-dose Series specific rules
*************************************************************************************************************************************************************************************/

/*************************************************************************************************************************************************************************************
MenB 4C 2-dose Series specific rules
*************************************************************************************************************************************************************************************/
rule "Mening B: When patient got 2 doses in the MenB 4C 2-dose Series of CVX 163 before Oct 24, 2024 end forecasting"
    agenda-group "RecommendationForecast^postCustomRecommendationCheck"
    salience -100
    activation-group "specialDoseRulesCheck"
    when
        // Series Conditions
        There is a Series $targetSeries
            - the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
            - the name of the series is "MenB4C2DoseSeries"
            - a forecast for the Series has been made and a shot is Recommended
            - the Series is not Complete
            - the effective dose number in the Series is == 3

        // Patient Information
        The Patient information $patientInfo must be known to complete writing this rule
            - make note of the Patient's birthdate as $birthdate

        // Previous Shot (First Dose) Conditions
        There is an administered shot $previousShot
            - the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
            - the shot is not ignored
            - the vaccine administered is "ICE163"
            - the administration date of the shot is < "24-Oct-2024"
            - that has already been evaluated and whose shot validity is VALID or ACCEPTED
            - make note of the date this shot was administered as $previousShotDate

        // Current Shot (Second Dose) Conditions
        There is an Administered Shot $currentShot
            - the shot belongs to the Series with name a member of ("MenB4C2DoseSeries")
            - the administration date of the shot is < "24-Oct-2024"
            - the administration date of the shot is > $previousShotDate
            - make note of the date this shot was administered as $currentShotDate
            - make note of the associated series as $associatedTargetSeries

        // Ensure no IceFact with below minimum interval
        There does not exist an IceFact
            - that has associated administered shot $currentShot
            - that has finding SupportedFactConcept._BELOW_MINIMUM_INTERVAL.conceptCodeValue

        // Age Validations - ensure patient was 16+ for both doses
        Confirm Elapsed time between $birthdate and $previousShotDate >= "16y-4d"
        Confirm Elapsed time between $birthdate and $currentShotDate >= "16y-4d"

        // Validate minimum interval between doses
        Confirm Elapsed time between $previousShotDate and $currentShotDate >= "1m-4d"

    then
        // Actions
        Clear forecasted recommendations from consideration in series $targetSeries
        Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
        Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.COMPLETE"
        Include the Recommendation $recommendation for Consideration in the final Forecast of the Series
        Mark Forecasting of the Series $targetSeries Complete
        Record that this Series Rule was Processed for the TargetSeries $targetSeries
        Log that this Series Rule fired for the Series $targetSeries
end

rule "Mening B: When High Risk Patient got 2 doses in the MenB 4C 2-dose Series of CVX 163 before Oct 24, 2024 end forecasting with Suplemental Text"
    agenda-group "RecommendationForecast^postCustomRecommendationCheck"
    salience -50
    activation-group "specialDoseRulesCheck"
    when
        // Series Conditions
        There is a Series $targetSeries
            - the Series belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
            - the name of the series is "MenB4C2DoseSeries"
            - a forecast for the Series has been made and a shot is Recommended
            - the Series is not Complete
            - the effective dose number in the Series is == 3

        // Patient Information
        The Patient information $patientInfo must be known to complete writing this rule
            - make note of the Patient's birthdate as $birthdate

        // Previous Shot (First Dose) Conditions
        There is an administered shot $previousShot
            - the shot belongs to the Vaccine Group "VACCINE_GROUP_CONCEPT.835"
            - the shot is not ignored
            - the vaccine administered is "ICE163"
            - the administration date of the shot is < "24-Oct-2024"
            - that has already been evaluated and whose shot validity is VALID or ACCEPTED
            - make note of the date this shot was administered as $previousShotDate

        // Current Shot (Second Dose) Conditions
        There is an Administered Shot $currentShot
            - the shot belongs to the Series with name a member of ("MenB4C2DoseSeries")
            - the administration date of the shot is < "24-Oct-2024"
            - the administration date of the shot is > $previousShotDate
            - make note of the date this shot was administered as $currentShotDate
            - make note of the associated series as $associatedTargetSeries

        // Ensure no IceFact with below minimum interval
        There does not exist an IceFact
            - that has associated administered shot $currentShot
            - that has finding SupportedFactConcept._BELOW_MINIMUM_INTERVAL.conceptCodeValue

        // Age Validations - ensure patient was 16+ for both doses
        Confirm Elapsed time between $birthdate and $previousShotDate >= "16y-4d"
        Confirm Elapsed time between $birthdate and $currentShotDate >= "16y-4d"

        // Validate minimum interval between doses
        Confirm Elapsed time between $previousShotDate and $currentShotDate >= "1m-4d"

        // Validate is High Risk patient
        Confirm the variable isMenBHighRisk is == true

    then
        // Actions
        Clear forecasted recommendations from consideration in series $targetSeries
        Create a Recommendation as $recommendation with Status RecommendationStatus.NOT_RECOMMENDED for the Series $targetSeries
        Set the Recommendation Reason for $recommendation to "RECOMMENDATION_REASON_CONCEPT.MENB_SC_HIGH_RISK_BOOSTER_RECOMMENDED"
        Include the Recommendation $recommendation for Consideration in the final Forecast of the Series

        // Supplemental Text Recommendation
        Create a recommendation as $rSupplementalText for the Series $targetSeries
        Set the Recommendation Supplemental Text for $rSupplementalText to "Recommended to receive a booster dose."
        Include the Recommendation $rSupplementalText for Consideration in the final Forecast of the Series

        // Finalize
        Mark Forecasting of the Series $targetSeries Complete
        Record that this Series Rule was Processed for the TargetSeries $targetSeries
        Log that this Series Rule fired for the Series $targetSeries
end
/*************************************************************************************************************************************************************************************
END MenB 4C 2-dose Series specific rules
*************************************************************************************************************************************************************************************/